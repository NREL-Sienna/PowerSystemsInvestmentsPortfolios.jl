"""
Utility functions to get individual attributes from custom NamedTuples.
"""
get_max(x::MinMax) = x.max
get_min(x::MinMax) = x.min
get_in(x::InOut) = x.in
get_out(x::InOut) = x.out

"""
Functions to calculate the existing capacity associated with a PSIP Technology.

If a technology has an ExistingCapacity supplemental attribute, it will use the names stored in the attribute
to retrieve relevant components from the base system and calculate their total rated capacity.
"""

"""
Functions to obtain the parameter type T from various Technology types.
"""
get_parameter_type(t::SupplyTechnology{T}) where {T} = T
get_parameter_type(::Type{SupplyTechnology{T}}) where {T} = T
get_parameter_type(t::StorageTechnology{T}) where {T} = T
get_parameter_type(::Type{StorageTechnology{T}}) where {T} = T
get_parameter_type(t::AggregateTransportTechnology{T}) where {T} = T
get_parameter_type(::Type{AggregateTransportTechnology{T}}) where {T} = T
get_parameter_type(t::NodalACTransportTechnology{T}) where {T} = T
get_parameter_type(::Type{NodalACTransportTechnology{T}}) where {T} = T
get_parameter_type(t::NodalHVDCTransportTechnology{T}) where {T} = T
get_parameter_type(::Type{NodalHVDCTransportTechnology{T}}) where {T} = T
get_parameter_type(t::DemandRequirement{T}) where {T} = T
get_parameter_type(::Type{DemandRequirement{T}}) where {T} = T
get_parameter_type(t::DemandSideTechnology{T}) where {T} = T
get_parameter_type(::Type{DemandSideTechnology{T}}) where {T} = T

"""
Calculates the amount of existing capacity (in MW) associated with a given Technology in the Portfolio.
Technology must have an ExistingCapacity supplemental attribute attached to it to be non-zero.
This attribute contains a list of names of existing assets in the base system that correspond to this technology.
For StorageTechnology, this function returns existing charge/discharge capacity in MW. See
[`get_existing_capacity_mwh`](@ref) for existing energy capacity in MWh.

# Arguments

  - `p::Portfolio`: The portfolio containing the base system and technology
  - `t::Union{ResourceTechnology, TransmissionTechnology}`: The technology to get existing capacity
"""
function get_existing_capacity_mw(
    p::Portfolio,
    t::Union{ResourceTechnology, TransmissionTechnology},
)
    if typeof(t) <: ColocatedSupplyStorageTechnology
        @warn "Co-located technologies are not supported for ExistingCapacity, assuming an existing capacity of 0"
        return 0.0
    end

    if !is_new(t)
        attr = IS.get_supplemental_attributes(ExistingCapacity, t)
        if length(attr) > 1
            @warn "Multiple ExistingCapacity attributes are attached to this technology, assuming a capacity of 0.0"
            return 0.0
        end

        gen_names = get_existing_technologies(only(attr))
        if length(gen_names) == 0
            @warn "No names listed in ExistingCapacity attribute, returning capacity of 0.0."
            return 0.0
        end

        comp = PSY.get_component.(get_parameter_type(t), Ref(p.base_system), gen_names)

        # Check if any of the components returned nothing
        filtered_comp = filter(x -> !isnothing(x), comp)
        if length(filtered_comp) != length(gen_names)
            @error "Not all names in ExistingCapacity matched generators in the base system"
        end

        return sum(PSY.get_rating(t) * PSY.get_base_power(p.base_system) for t in comp)
    else
        return 0.0
    end
end

"""
Calculates the amount of existing energy capacity (in MWh) associated with a given StorageTechnology.
Technology must have an ExistingCapacity supplemental attribute attached to it to be non-zero.
This attribute contains a list of names of existing assets in the base system that correspond to this technology.
To get the charge/discharge capacity in MW, see [`get_existing_capacity_mw`](@ref).

# Arguments

  - `p::Portfolio`: The portfolio containing the base system and technology
  - `t::StorageTechnology`: The storage technology to get existing energy capacity
"""
function get_existing_capacity_mwh(p::Portfolio, t::StorageTechnology)
    if !is_new(t)
        attr = IS.get_supplemental_attributes(ExistingCapacity, t)
        if length(attr) > 1
            @warn "Multiple ExistingCapacity attributes are attached to this technology, assuming a capacity of 0.0"
            return 0.0
        end
        gen_names = get_existing_technologies(only(attr))
        comp = PSY.get_component.(get_parameter_type(t), Ref(p.base_system), gen_names)

        # Check if any of the components returned nothing
        filtered_comp = filter(x -> !isnothing(x), comp)
        if length(filtered_comp) != length(gen_names)
            @error "Not all names in ExistingCapacity matched generators in the base system"
        end

        return sum(
            PSY.get_storage_capacity(t) * PSY.get_base_power(p.base_system) for t in comp
        )
    else
        return 0.0
    end
end

"""
Determines if a technology is a completely new build and is not associated with any pre-existing capacity
"""
is_new(t::Technology) = !(IS.has_supplemental_attributes(ExistingCapacity, t))

"""
Get constant heat rate value from FuelCurve stored in a SupplyTechnology
"""
get_heat_rate(t::SupplyTechnology) =
    IS.get_proportional_term(IS.get_value_curve(PSY.get_variable(get_operation_costs(t))))

"""
Get constant fuel cost from FuelCurve stored in a SupplyTechnology
"""
get_fuel_cost(t::SupplyTechnology) =
    IS.get_fuel_cost(PSY.get_variable(get_operation_costs(t)))

"""
Get constant variable OM costs from OperationalCost in a SupplyTechnology
"""
get_variable_cost(t::SupplyTechnology) =
    IS.get_proportional_term(IS.get_vom_cost(PSY.get_variable(get_operation_costs(t))))

"""
Get constant variable OM costs for storage charging from OperationalCost in a StorageTechnology
"""
get_variable_cost_charge(t::StorageTechnology) = PSY.get_proportional_term(
    PSY.get_vom_cost(PSY.get_charge_variable_cost(get_operation_costs(t))),
)

"""
Get constant variable OM costs for storage discharging from OperationalCost in a StorageTechnology
"""
get_variable_cost_discharge(t::StorageTechnology) = PSY.get_proportional_term(
    PSY.get_vom_cost(PSY.get_discharge_variable_cost(get_operation_costs(t))),
)

"""
Get constant fixed OM costs from OperationalCost for a SupplyTechnology
"""
get_fixed_cost(t::Technology) = PSY.get_fixed(get_operation_costs(t))

"""
Get constant fixed OM costs for storage charge from OperationalCost in a StorageTechnology
"""
get_fixed_cost_charge(t::StorageTechnology) = PSY.get_proportional_term(
    PSY.get_value_curve(PSY.get_charge_variable_cost(get_operation_costs(t))),
)

"""
Get constant fixed OM costs for storage discharge from OperationalCost in a StorageTechnology
"""
get_fixed_cost_discharge(t::StorageTechnology) = PSY.get_proportional_term(
    PSY.get_value_curve(PSY.get_discharge_variable_cost(get_operation_costs(t))),
)

"""
Calculate the weighted average cost of capital (WACC) for a Technology based on its TechnologyFinancialData.
We defined WACC such that `WACC = D * Rd * (1 - Tc) + E * Re``
where D is the debt fraction, Rd is the debt rate, Tc is the tax rate, E is the equity fraction, and Re is the return on equity.

Will return an error if the debt fraction is not between 0.0 and 1.0.
"""
function get_wacc(tech_financials::TechnologyFinancialData)
    dr = tech_financials.debt_rate
    tr = tech_financials.tax_rate
    re = tech_financials.return_on_equity
    df = tech_financials.debt_fraction
    ef = 1.0 - df
    if df > 1.0 || df < 0.0
        error("Debt fraction must be between 0.0 and 1.0")
    end
    return df * dr * (1.0 - tr) + ef * re
end
